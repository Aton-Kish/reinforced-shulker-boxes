plugins {
	id 'fabric-loom' version '0.9-SNAPSHOT'
	id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

ext {
	reinforcedCoreVersion = "1.0.2"
	quickShulkerVersion = "1.2.2-1.17"
	shulkerBoxTooltipVersion = "3.0.1+1.17"
	modMenuVersion = "2.0.2"
	clothConfigVersion = '5.0.38'
	fabricAsmVersion = "2.0"
}

repositories {
	// Add repositories to retrieve artifacts from in here.
	// You should only use this when depending on other mods because
	// Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
	// See https://docs.gradle.org/current/userguide/declaring_repositories.html
	// for more information about repositories.

	// Reinforced Core
    maven {
        url "https://raw.githubusercontent.com/Aton-Kish/mcmod/maven"
    }

	// Quick Shulker
	maven {
		url "https://maven.kyrptonaught.dev"
	}

	// Shulker Box Tooltip
	maven {
		url "https://maven.misterpemodder.com/libs-release"
	}

	// Mod Menu (Shulker Box Tooltip deps)
    maven {
        url "https://raw.githubusercontent.com/TerraformersMC/Archive/main/releases"
    }

	// Cloth Config (Shulker Box Tooltip deps)
	maven {
        url "https://maven.shedaniel.me"
    }

	// Fabric ASM (Shulker Box Tooltip deps)
	maven {
		url "https://jitpack.io"
	}
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	// PSA: Some older mods, compiled on Loom 0.2.1, might have outdated Maven POMs.
	// You may need to force-disable transitiveness on them.

	// Reinforced Core
	modImplementation("atonkish.reinfcore:reinforced-core:${reinforcedCoreVersion}") {
        exclude(group: "net.fabricmc.fabric-api")
	}
	include "atonkish.reinfcore:reinforced-core:${reinforcedCoreVersion}"

	// Quick Shulker
	modImplementation("net.kyrptonaught:quickshulker:${quickShulkerVersion}") {
        exclude(group: "net.fabricmc.fabric-api")
    }

	// Shulker Box Tooltip
	modImplementation("com.misterpemodder:shulkerboxtooltip:${shulkerBoxTooltipVersion}") {
        exclude(group: "net.fabricmc.fabric-api")
	}

	// Mod Menu (Shulker Box Tooltip deps)
    modImplementation("com.terraformersmc:modmenu:${modMenuVersion}") {
        exclude(group: "net.fabricmc.fabric-api")
    }

	// Cloth Config (Shulker Box Tooltip deps)
	modApi("me.shedaniel.cloth:cloth-config-fabric:${clothConfigVersion}") {
        exclude(group: "net.fabricmc.fabric-api")
    }

	// Fabric ASM (Shulker Box Tooltip deps)
	modImplementation ("com.github.Chocohead:Fabric-ASM:${fabricAsmVersion}") {
		exclude(group : "net.fabricmc.fabric-api")
	}
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	// ensure that the encoding is set to UTF-8, no matter what the system default is
	// this fixes some edge cases with special characters not displaying correctly
	// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
	// If Javadoc is generated, this must be specified in that task too.
	it.options.encoding = "UTF-8"

	// Minecraft 1.17 (21w19a) upwards uses Java 16.
	it.options.release = 16
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			// add all the jars that should be included when publishing to maven
			artifact(remapJar) {
				builtBy remapJar
			}
			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
		maven {
			url "file:/$buildDir/maven"
		}
	}
}
